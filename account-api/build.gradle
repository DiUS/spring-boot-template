buildscript {
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springbootVersion")
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
    }
}

plugins {
    id "au.com.dius.pact" version "3.5.24"
    id "com.wiredforcode.spawn" version "0.8.2"
}

apply plugin: "org.springframework.boot"
apply plugin: "com.wiredforcode.spawn"

jar {
    baseName = 'account-api'
    version =  "$version"
}

dependencies {
    compile(
        "org.springframework.boot:spring-boot-starter-parent:$springbootVersion",
        "org.springframework.boot:spring-boot-starter-web:$springbootVersion",
        "org.springframework.boot:spring-boot-starter-jersey:$springbootVersion",
        "javax.xml.bind:jaxb-api:2.3.1",
        "org.slf4j:slf4j-api:$slf4jVersion"
    )

    testCompile (
        "org.assertj:assertj-core:$assertjVersion",
        "org.springframework.boot:spring-boot-starter-test:$springbootVersion"
    )
}

import com.wiredforcode.gradle.spawn.*
task startProvider(type: SpawnProcessTask, dependsOn: 'assemble') {
    command "java -Dspring.profiles.active=test -jar ${jar.archivePath}"
    ready 'Started Application'
}

task stopProvider(type: KillProcessTask) { }

pact {
    serviceProviders {
        accountProvider {

            port = 8080
            startProviderTask = startProvider
            terminateProviderTask = stopProvider
            stateChangeUrl = url('http://localhost:8080/pactStateChange')

            hasPactsWith('Account_Consumer') {
                pactFileLocation = file("$projectDir/../pacts")
            }
        }
    }
}